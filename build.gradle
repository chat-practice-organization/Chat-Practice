buildscript {
    ext {
        springBootVersion = '3.0.6'
    }

    repositories {
        mavenCentral()
    }
    // 빌드 스크립트에 필요한 외부 종속성을 선언합니다. 여기서는 Spring Boot Gradle 플러그인 및 Spring의 의존성 관리 플러그인에 대한 의존성을 설정합니다.
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.1.0.RELEASE"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.practice'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-amqp'
        implementation 'org.springframework.boot:spring-boot-starter-websocket'
        implementation 'org.apache.kafka:kafka-clients'
        implementation 'org.springframework.kafka:spring-kafka'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap:3.0.4'
        implementation 'org.springframework.cloud:spring-cloud-starter-aws-secrets-manager-config:2.2.6.RELEASE'
        implementation 'org.postgresql:postgresql'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        compileOnly 'org.projectlombok:lombok'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.amqp:spring-rabbit-test'
        testImplementation 'org.springframework.kafka:spring-kafka-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':chat-application') {
    bootJar.enabled = true
    jar.enabled = false

    dependencies {
        implementation project(':common')
    }
}

project(':message-classifier') {
    bootJar.enabled = true
    jar.enabled = false

    dependencies {
        implementation project(':common')
    }
}

